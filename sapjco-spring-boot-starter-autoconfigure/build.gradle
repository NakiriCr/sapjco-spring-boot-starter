plugins {
    id 'java'
}
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: rootDir.canonicalPath + '/artifact.gradle'

group 'cn.yanzx-dev.gitlab'
version artifactVersion

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compile group: 'com.github.virtualcry', name: 'sapjco-spring', version: '3.2.0'
    compile group: 'org.springframework.boot', name: 'spring-boot', version: '1.4.0.RELEASE'
    
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.4.0.RELEASE'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: '1.4.0.RELEASE'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cn.yanzx-dev.gitlab'
            artifactId = 'sapjco-spring-boot-starter-autoconfigure'
            version = artifactVersion
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // artifact name
                name = 'Sapjco Spring Boot Starter AutoConfigure'
                // artifact description
                description = 'Sapjco Spring Boot Starter AutoConfigure'
                // artifact homepage
                url = 'https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring-boot-starter'
                // licenses
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // developers info
                developers {
                    developer {
                        name = 'VirtualCry'
                        email = '172519267@qq.com'
                    }
                }
                // scm
                scm {
                    url = 'https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring-boot-starter'
                    connection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring-boot-starter.git'
                    developerConnection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring-boot-starter.git'
                }
            }
        }
    }
    // repositories to publish
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jar {
    manifest {
        attributes 'Built-By': 'VirtualCry'
        attributes 'Created-By': 'Gradle-' + gradle.getGradleVersion()
        attributes 'Build-Jdk': java.getTargetCompatibility()
    }
}
